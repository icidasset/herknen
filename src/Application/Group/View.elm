module Group.View exposing (..)

import Common.View as Common
import Css.Classes as C
import Group exposing (Group)
import Html exposing (Html)
import Html.Attributes as A
import Html.Events as E
import Radix exposing (..)
import Unit exposing (Unit)
import Url



-- ðŸŒ„


index : List Group -> Html Msg
index groups =
    Common.index
        [ listView groups

        --
        , if List.isEmpty groups then
            Common.emptyState
                [ E.onClick CreateGroup
                , C.cursor_pointer
                ]
                [ Html.p
                    []
                    [ Html.span [ C.sm__hidden ] [ Html.text "Tap the screen" ]
                    , Html.span [ C.hidden, C.sm__inline ] [ Html.text "Click here" ]
                    , Html.br [] []
                    , Html.text "to make your first list."
                    ]
                ]

          else
            Common.create
                { withBorder = List.isEmpty groups }
                [ A.title "Create a new list"
                , E.onClick CreateGroup
                ]
        ]



-- GROUP


listView : List Group -> Html Msg
listView groups =
    groups
        |> List.indexedMap
            (\idx group ->
                Common.item
                    { complete = CompleteGroup
                    , edit = EditGroup
                    , finishedEditing = FinishedEditingGroup
                    , gestureTarget = StartGroupGesture
                    , input = UpdateGroupLabel
                    , remove = RemoveGroup
                    }
                    Html.a
                    [ A.href ("#/group/" ++ Url.percentEncode group.label) ]
                    idx
                    { group | isDone = List.all .isDone group.units && not (List.isEmpty group.units) }
            )
        |> Common.list
